import org.moallemi.gradle.internal.VersionCodeType

apply plugin: 'com.android.application'
apply plugin: 'org.moallemi.advanced-build-version'
apply plugin: 'jacoco'

advancedVersioning {
    nameOptions {
        versionPatch versionCode
    }
    codeOptions {
        versionCodeType VersionCodeType.AUTO_INCREMENT_ONE_STEP
        dependsOnTasks 'assemble'
    }
    outputOptions {
        renameOutput false
        nameFormat 'Transformer-${buildType}-${versionName}'
    }
}

def appVersionName = advancedVersioning.versionName
def appVersionCode = advancedVersioning.versionCode
def keyStore = file('transformer.keystore')

android {
    compileSdkVersion 25
    buildToolsVersion gradle.buildToolsVersion

    defaultConfig {
        applicationId "me.drakeet.transformer"
        minSdkVersion 14
        targetSdkVersion 25
        versionCode appVersionCode
        versionName appVersionName
        jackOptions {
            enabled true
        }
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
    }

    dexOptions {
        dexInProcess = true
    }

    compileOptions {
        incremental = true
    }

    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }

    signingConfigs {
        app {
            storeFile file('transformer.keystore')
            storePassword project.hasProperty('STOREPASS') ? STOREPASS : ''
            keyAlias project.hasProperty('KEYALIAS') ? KEYALIAS : ''
            keyPassword project.hasProperty('KEYPASS') ? KEYPASS : ''
        }
    }

    buildTypes {
        debug {
            buildConfigField "boolean", "LOG_DEBUG", "true"
            debuggable true
            applicationIdSuffix ".debug"
        }

        release {
            debuggable false
            minifyEnabled false
            shrinkResources false
            proguardFile getDefaultProguardFile('proguard-android.txt')
            proguardFile 'proguard-android-patch.pro'
            proguardFile 'proguard-rules.pro'

            if (keyStore.exists()) {
                println "transformer: using drakeet's key"
                signingConfig signingConfigs.app
            } else {
                println "transformer: using default key"
                signingConfig signingConfigs.debug
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    compile project(':timemachine')
    compile project(':timemachine-widget')
    compile project(':timemachine-store')
    compile "com.android.support:appcompat-v7:$gradle.supportLibraryVersion"

    compile "com.google.android.agera:agera:$gradle.ageraVersion"
    compile "com.google.android.agera:net:$gradle.ageraVersion"
    compile "com.google.android.agera:content:$gradle.ageraVersion"
    compile 'me.drakeet.agera:bus:1.0.0'
    compile 'com.google.code.gson:gson:2.7'

    debugCompile "com.squareup.leakcanary:leakcanary-android:$gradle.leakCanaryVersion"
    debugCompile "me.drakeet.library:crashwoodpecker:$gradle.crashwoodpeckerVersion"

    releaseCompile "com.squareup.leakcanary:leakcanary-android-no-op:$gradle.leakCanaryVersion"
    releaseCompile "me.drakeet.library:crashwoodpecker-do-nothing:$gradle.crashwoodpeckerVersion"

    androidTestCompile "com.android.support:appcompat-v7:$gradle.supportLibraryVersion"
    androidTestCompile "com.android.support:design:$gradle.supportLibraryVersion"

    compile 'com.android.support.test.espresso:espresso-idling-resource:2.2.2'

    androidTestCompile 'com.android.support.test.espresso:espresso-contrib:2.2.2'
    androidTestCompile 'com.android.support.test.espresso:espresso-core:2.2.2'
    androidTestCompile 'com.android.support.test:runner:0.5'
    androidTestCompile 'com.android.support.test:rules:0.5'
}
